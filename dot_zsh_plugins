export ZPLUGINDIR=$ZDOTDIR/.config/zsh/plugins

plugins=(
   romkatv/zsh-defer
   zsh-users/zsh-completions
   zsh-users/zsh-autosuggestions
   zsh-users/zsh-syntax-highlighting
   zsh-users/zsh-history-substring-search
   MichaelAquilina/zsh-autoswitch-virtualenv
   MichaelAquilina/zsh-you-should-use
   hlissner/zsh-autopair
   laggardkernel/git-ignore
   ptavares/zsh-direnv
   sunlei/zsh-ssh
   Aloxaf/fzf-tab
   omz/fancy-ctrl-z
   omz/sudo
   omz/copypath
   omz/git-auto-fetch
)

function plugin-load {
   # Create plugin directory if it doesn't exist
   [[ -d $ZPLUGINDIR ]] || mkdir -p "$ZPLUGINDIR"

   for plugin in $plugins; do
      if [[ ! -d $ZPLUGINDIR/${plugin:t} ]]; then
         if [[ ${plugin:h} == 'omz' ]]; then
            curl https://raw.githubusercontent.com/ohmyzsh/ohmyzsh/master/plugins/"${plugin:t}"/"${plugin:t}".plugin.zsh \
               --create-dirs --silent --show-error \
               --output "$ZPLUGINDIR"/"${plugin:t}"/"${plugin:t}".plugin.zsh
         else
            git clone https://github.com/"${plugin}" "$ZPLUGINDIR"/"${plugin:t}"
         fi
      fi

      # Find first matching init file
      local plugin_dir="$ZPLUGINDIR/${plugin:t}"
      local initfile="$plugin_dir/${plugin:t}.plugin.zsh"

      if [[ ! -e $initfile ]]; then
         local initfiles=($plugin_dir/*.{plugin.zsh,zsh-theme,zsh,sh}(N))
         if (($#initfiles == 0)); then
            echo >&2 "No init file found for '$plugin'."
            continue
         fi
         ln -sf "${initfiles[1]}" "$initfile"
      fi
      fpath+=$plugin_dir
      (( $+functions[zsh-defer] )) && zsh-defer . "$initfile" || . "$initfile"
   done
}

function plugin-list {
   for d in $ZPLUGINDIR/*/.git; do
      git -C "${d:h}" remote get-url origin
   done
}

function plugin-compile {
   local f
   autoload -Uz zrecompile
   for f in $ZPLUGINDIR/**/*.zsh; do
      [[ $f != */test-data/* ]] || continue # fix for zsh-syntax-highlighting
      zrecompile -pq "$f"
   done
}

function plugin-update {
   for d in $ZPLUGINDIR/*/.git(/); do
     echo "Updating ${d:h:t}..."
     command git -C "${d:h}" pull --ff --recurse-submodules --depth 1 --rebase --autostash
   done
}

plugin-load
plugin-compile
